buildscript {
    repositories {
        jcenter()

        // Add SpotBugs required repo
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.ysb33r.gradle:doxygen:0.2"

        // Add SpotBugs
        classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
    }
}

apply plugin: "java"
//apply plugin: "application"
apply plugin: "eclipse"
apply plugin: "org.ysb33r.doxygen"
apply plugin: "jacoco"

apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "com.github.spotbugs"

apply plugin: "checkstyle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

// Configure Doxygen task
doxygen {
    generate_html true

    template "documentation.config"

    source new File(projectDir,"src/main/java")
    //source new File(projectDir,"src/test/java")
    outputDir new File(buildDir,"docs")
}

/**
 * Build cmdline jar
 */
task jarCmd(type: Jar) {
    from(sourceSets.main.output) {
        include "**/*.class"
    }

    baseName = "TestPrimeGenerator"

    manifest {
        attributes(
          "Main-Class": "edu.odu.cs.cs330.examples.guithread.driver.TestPrimeGenerator"
        )
    }
}

/**
 * Build GUI jar
 */
task jarGui(type: Jar) {
    from(sourceSets.main.output) {
        include "**/*.class"
    }

    baseName = "PrimeGui"

    manifest {
        attributes(
          "Main-Class": "edu.odu.cs.cs330.examples.guithread.driver.PrimeGui"
        )
    }
}

/**
 * Build Gui Thread jar
 */
task jarThd(type: Jar) {
    from(sourceSets.main.output) {
        include "**/*.class"
    }

    baseName = "PrimeGuiThread"

    manifest {
        attributes(
          "Main-Class": "edu.odu.cs.cs330.examples.guithread.driver.PrimeGuiThread"
        )
    }
}

task deploy(dependsOn: [
            jarCmd,
            jarGui,
            jarThd,
            doxygen,
            javadoc]){
}

/**
 * Run code style/error checks
 */
findbugsMain {
    ignoreFailures true
    reports {
        xml.enabled false
        html.enabled true
    }
}

// SpotBugs
spotbugsMain {
    ignoreFailures = true
    effort = "max"
    reportLevel = "medium"
    reports {
       xml.enabled = false
       html.enabled = true
    }
}

spotbugsTest.enabled = false
// End SpotBugs config

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
    }
}

pmd {
    ignoreFailures = true
    ruleSets = [
        "java-basic",
        "java-braces",
        //"java-clone",
        "java-codesize",
        //"java-comments",
        //"java-controversial",
        //"java-coupling",
        //"java-design",
        "java-empty",
        //"java-finalizers",
        "java-imports",
        //"java-junit",
        "java-naming",
        //"java-optimizations",
        "java-strictexception",
        "java-strings",
        "java-sunsecure",
        "java-typeresolution",
        "java-unnecessary",
        "java-unusedcode"
    ]
}

// Check Style Config
checkstyle {
    ignoreFailures = true
    showViolations = false
}

tasks.withType(Checkstyle) {
    reports {
        html.destination project.file("build/reports/checkstyle/main.html")
    }
}
checkstyleTest.enabled = false
// End Checkstyle config
